apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

ext {
    def appcompatVersion = "1.2.0"
    def ktxCoreVersion = "1.3.2"
    def constraintLayoutVersion = "2.0.3"
    def navigationVersion = "2.3.1"
    def roomVersion = "2.2.5"
    def lifecycleVersion = "2.2.0"
    def pagingVersion = "2.1.2"

    androidX = [
            "appcompat"         : "androidx.appcompat:appcompat:$appcompatVersion",
            "ktxCore"           : "androidx.core:core-ktx:$ktxCoreVersion",
            "constraintLayout"  : "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion",
            "navigationFragment": "androidx.navigation:navigation-fragment-ktx:$navigationVersion",
            "navigationUi"      : "androidx.navigation:navigation-ui-ktx:$navigationVersion",
            "room"              : "androidx.room:room-runtime:$roomVersion",
            "roomKtx"           : "androidx.room:room-ktx:$roomVersion",
            "lifecycle"         : "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion",
            "lifecycleKtx"      : "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion",
            "viewmodel"         : "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion",
            "liveData"          : "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion",
            "paging"            : "androidx.paging:paging-runtime-ktx:$pagingVersion"
    ]

    def materialDesignVersion = "1.2.1"
    def daggerVersion = "2.28"

    google = [
            "design": "com.google.android.material:material:$materialDesignVersion",
            "dagger": "com.google.dagger:dagger:$daggerVersion",
    ]

    def retrofitVersion = "2.9.0"
    def moshiVersion = "1.9.2"
    def okHttpLoggingInterceptorVersion = "4.8.0"

    square = [
            "retorift"      : "com.squareup.retrofit2:retrofit:$retrofitVersion",
            "moshiConverter": "com.squareup.retrofit2:converter-moshi:$retrofitVersion",
            "moshi"         : "com.squareup.moshi:moshi:$moshiVersion",
            "okhttp"        : "com.squareup.okhttp3:logging-interceptor:$okHttpLoggingInterceptorVersion"
    ]

    kapts = [
            "dagger": "com.google.dagger:dagger-compiler:$daggerVersion",
            "room"  : "androidx.room:room-compiler:$roomVersion",
            "moshi" : "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    ]


    def junitVersion = "4.12"

    testDependencies = [
            "junit": "junit:junit:$junitVersion"
    ]

    def junitAndroidVersion = "1.1.2"
    def espressoVersion = "3.3.0"
    androidTestDependencies = [
            "junit"   : "androidx.test.ext:junit:$junitAndroidVersion",
            "espresso": "androidx.test.espresso:espresso-core:$espressoVersion"
    ]
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.tanay.githubrepoviewer"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation androidX.values()
    implementation google.values()
    implementation square.values()
    kapt kapts.values()
    testImplementation testDependencies.values()
    androidTestImplementation androidTestDependencies.values()

}